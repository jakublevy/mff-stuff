module MainBrick(mainBrick) where

import Brick
import qualified Brick.Types as T
import Brick.Util (bg, fg, on)
import Brick.Widgets.Border (border)
import Brick.Widgets.Center (center)
import Brick.BChan
import qualified Graphics.Vty as V
import qualified Graphics.Vty.Attributes as Color
import Network.Socket
import Control.Monad.IO.Class (liftIO)
import qualified Control.Exception as E
import Control.Monad(forever, void)
import Control.Concurrent(killThread, threadDelay, forkIO)
import Data.Maybe(fromJust)
import System.IO
import Data.Set(empty)

import Game
import GameTypes

data Tick = Tick

{- this is for creating the initial state -}
initState :: ScreenState
initState = ScreenState { curPos = (0, 0)
                        , curHighlighted = True
                        , cells = empty
                        , width = defaultWidth 
                        , height = defaultHeight
                        }

putWriterReader :: Writer -> Reader -> ScreenState -> ScreenState
putWriterReader w r s = s { writer = w, reader = r}

sendMsg :: ScreenState -> String -> IO ()
sendMsg s = write (writer s)

receiveMsg :: ScreenState -> IO (Maybe String)
receiveMsg s = lastMsg (reader s) 

quitBricks :: ScreenState -> EventM () (Next ScreenState)
quitBricks s = liftIO (sendMsg s "quit") >> halt s

{- This function converts the state to a list of widget layers to be drawn,
 - we only draw a single layer. -}
renderApp :: ScreenState -> [Widget n]
renderApp s = [center (title <=> mainLayer s)]

{- Operator <=> combines widgets vertically (as seen above),
 - operator <+> combines them horizontally. -}
-- mainLayer s = foldr1 (<+>) $ map (letter s) [0 .. 10]
mainLayer :: ScreenState -> Widget n
mainLayer s =
  border $
  foldr1
    (<+>)
    [ line2widget s [(x, y) | y <- [0 .. height s - 1]]
    | x <- [0 .. width s - 1]
    ]

line2widget :: ScreenState -> [Pos] -> Widget n
line2widget s xs = foldr1 (<=>) $ map (pos2widget s) xs

pos2widget :: ScreenState -> Pos -> Widget n
pos2widget s pos
  | isAlive s pos =
    if curHighlighted s && pos == curPos s
      then withAttr cursorHl $ str "X"
      else str "X"
  | otherwise =
    if curHighlighted s && pos == curPos s
      then withAttr cursorHl $ str " "
      else str " "

title :: Widget n
title = border $ str "Game of Life"

cursorHl :: AttrName
cursorHl = attrName "cursorHl"

{- 
 - Event handlers that modify the state. They should in fact return a monad,
 - but that is vastly simplified by the helper functions:
 -
 - halt newstate   -- halts the app
 - continue newstate    -- continues executing the app with the supplied state
 -
 - The record update syntax is used as such:
 - originalItem{modifiedField = new value}
 -}
handleEvent :: ScreenState -> BrickEvent () Tick -> EventM () (Next ScreenState)
handleEvent s (AppEvent Tick) = do
                                msgM <- liftIO $ receiveMsg s
                                case words <$> msgM of 
                                  Just ws -> if head ws == "board" then
                                                  continue $ s { cells = conv s $ ws !! 1 }
                                             else continue s
                                  Nothing -> continue s

handleEvent s (T.VtyEvent (V.EvKey k [])) =
  case k of
    V.KEsc -> quitBricks s
    V.KLeft -> move L s
    V.KRight -> move R s
    V.KUp -> move U s
    V.KDown -> move D s
    V.KChar 'q' -> quitBricks s
    V.KChar 'h' -> move L s
    V.KChar 'j' -> move D s
    V.KChar 'k' -> move U s
    V.KChar 'l' -> move R s
    V.KChar 'v' -> continue $ s {curHighlighted = not (curHighlighted s)}
    V.KChar 'x' -> do
                   let pos = map show $ (\(x,y) -> [x,y]) $ curPos s
                   liftIO . sendMsg s $ "flip " ++ pos!!0 ++ " " ++ pos!!1
                   continue s

    V.KChar ' ' -> liftIO (sendMsg s "step") >> continue s
                 
    _ -> continue s --ignore all other keys
handleEvent s _ = continue s --also ignore all other events

move :: Dir -> ScreenState -> EventM () (Next ScreenState)
move d s
  | d == L = continue $ s {curPos = (max 0 (x - 1), y)}
  | d == R = continue $ s {curPos = (min (width s - 1) (x + 1), y)}
  | d == U = continue $ s {curPos = (x, max 0 (y - 1))}
  | d == D = continue $ s {curPos = (x, min (height s - 1) (y + 1))}
  where
    x = fst $ curPos s
    y = snd $ curPos s

{- This constructs the Brick app from the available functions -}
app :: App ScreenState Tick ()
app =
  App
    { appDraw = renderApp
    , appChooseCursor = showFirstCursor
    , appHandleEvent = handleEvent
    , appStartEvent = return
    , appAttrMap =
        const $ attrMap V.defAttr [(cursorHl, Color.black `on` Color.cyan)]
    }

{- main just starts Brick main and discards (using void) the final state -}
mainBrick :: Socket -> IO ()
mainBrick sck = do
       h <- socketToHandle sck ReadWriteMode
       hSetBuffering h (BlockBuffering Nothing) --we will flush manually

       chan <- newBChan 10
       timerTId <- forkIO . forever $ do
                                      writeBChan chan Tick
                                      threadDelay 10000 --10ms

       initVty <- V.mkVty V.defaultConfig
       E.bracket
          (do 
           w <- initWriter h
           rInf <- initReader h
           return (w,rInf)
          )
          (\(w,rInf) -> do 
                        killThread timerTId 
                        stopWriter w 
                        killThread $ snd rInf 
                        hClose h
          )
          (\(w,rInf) -> void $ customMain initVty (V.mkVty V.defaultConfig) (Just chan) app (putWriterReader w (fst rInf) initState)
          )